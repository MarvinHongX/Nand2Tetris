// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // pre-setting the x input
    Mux16(a=x, b=false, sel=zx, out=zeroedX);
    Not16(in=zeroedX, out=notZeroedX);
    Mux16(a=zeroedX, b=notZeroedX, sel=nx, out=presetX);

    // pre-setting the y input
    Mux16(a=y, b=false, sel=zy, out=zeroedY);
    Not16(in=zeroedY, out=notZeroedY);
    Mux16(a=zeroedY, b=notZeroedY, sel=ny, out=presetY);
    
    // selecting between computing + or &
    And16(a=presetX, b=presetY, out=xAndy); // x&y
    Add16(a=presetX, b=presetY, out=xAddy);  // x+y
    Mux16(a=xAndy, b=xAddy, sel=f, out=computedXY);

    // post-setting the output
    Not16(in=computedXY, out=notComputedXY);
    Mux16(a=computedXY, b=notComputedXY, sel=no, out=out, out[0..7]=outLow, out[8..15]=outHigh, out[15]=ng);

    // zr, ng
    Or8Way(in=outLow, out=orLow);
    Or8Way(in=outHigh, out=orHigh);
    Or(a=orLow, b=orHigh, out=orout);
    Not(in=orout, out=zr);
}